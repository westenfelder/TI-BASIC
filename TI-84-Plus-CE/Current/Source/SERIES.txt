ClrHome
Clear Entries
Lbl M0
Menu("Series","Arithmetic",A0,"Geometric",B0,"Quadratic",C0,"Cyclic",D0,"Other",E0,"Exit",Z0
Lbl A0
Disp "Input first 2 terms:
Disp "
Input "a1=?",A
Input "a2=?",B
B-A→I
A-I→C
Lbl A1
ClrHome
Menu("Arithmetic Series","Display formula",A2,"Find nth term",A3,"Sum of first n terms",A4,"Sum of range",A5,"Back",M0
Lbl A2
ClrHome
Disp "an = a1 + d(n-1)
Disp "
Output(3,1,"a1= "
Output(3,4,C
Output(4,1,"d ="
Output(4,4,I
Pause 
Goto A1
Lbl A3
ClrHome
Input "n=?",N
Disp "
Output(3,1,A+I(N-1))
Pause 
Goto A1
Lbl A4
ClrHome
Input "n=?",N
Disp "
Output(3,1,(N/2)*(2A+IN-I))
Pause 
Goto A1
Lbl A5
ClrHome
Input "n-start=?",N
Input "n-end=?",P
Disp "
Output(4,1,(P/2)*(2A+IP-I)-((N-1)/2)*(2A+IN-2I)
Pause 
Goto A1
Lbl B0
Disp "Input first 2 terms:
Disp "
Input "a1=?",A
Input "a2=?",B
B/A→R
Lbl B1
ClrHome
Menu("Geometric Series","Display formula",B2,"Find nth term",B3,"Sum of first n terms",B4,"Sum of range",B5,"Back",M0
Lbl B2
ClrHome
Disp "an = a1 * r^(n-1)
Disp "
Output(3,1,"a1= "
Output(3,4,A
Output(4,1,"r ="
Output(4,4,R
Pause 
Goto B1
Lbl B3
ClrHome
Input "n=?",N
Disp "
Output(3,1,AR^(N-1)
Pause 
Goto B1
Lbl B4
ClrHome
Input "n=?",N
Disp "
Output(3,1,(A(1-R^N))/(1-R)
Pause 
Goto B1
Lbl B5
ClrHome
Input "n-start=?",N
Input "n-end=?",P
Disp "
Output(4,1,(A(1-R^P))/(1-R)-(A(1-R^(N-1)))/(1-R)
Pause 
Goto B1
Lbl C0
Disp "Input first 3 terms:
Disp "
Input "a1=?",D
Input "a2=?",E
Input "a3=?",F
{3,4}→dim([J])
1→[J](1,1):1→[J](1,2):1→[J](1,3):D→[J](1,4)
4→[J](2,1):2→[J](2,2):1→[J](2,3):E→[J](2,4)
9→[J](3,1):3→[J](3,2):1→[J](3,3):F→[J](3,4)
rref([J])→[J]
[J](1,4)→A:[J](2,4)→B:[J](3,4)→C
Lbl C1
ClrHome
Menu("Quadratic Series","Display formula",C2,"Find nth term",C3,"Sum of first n terms",C4,"Sum of range",C5,"Back",M0
Lbl C2
ClrHome
Disp "an = An^2 + Bn + C
Disp "
Output(3,1,"A= "
Output(3,4,A
Output(4,1,"B ="
Output(4,4,B
Output(5,1,"C ="
Output(5,4,C
Pause 
Goto C1
Lbl C3
ClrHome
Input "n=?",N
Disp "
Output(3,1,AN^2+NB+C
Pause 
Goto C1
Lbl C4
ClrHome
Input "n=?",N
Disp "
Output(3,1,AN(N+1)(2N+1)/6+BN(N+1)/2+CN
Pause 
Goto C1
Lbl C5
ClrHome
Input "n-start=?",P
Input "n-end=?",N
Disp "
Output(4,1,(AN(N+1)(2N+1)/6+BN(N+1)/2+CN)-(AP(P-1)(2P-1)/6+BP(P-1)/2+C(P-1))
Pause 
Goto C1
Lbl D0
Disp "Input list of repeating terms:
Disp "For eg, 3,4,5,0,8,3,4,5,0,8,...
Disp "Input {3,4,5,0,8}
Disp "
Input "List?",⌊CYC
dim(⌊CYC)→D
Lbl D1
ClrHome
Menu("Cyclic Series","Show series",D2,"Find nth term",D3,"Sum of first n terms",D4,"Back",M0
Lbl D2
ClrHome
Disp "sc=
Disp "
Disp ⌊CYC
Pause 
Goto D1
Lbl D3
ClrHome
Input "n=?",N
Disp "
remainder(N,D)→R
If R=0
D→R
Output(3,1,⌊CYC(R)
Pause 
Goto D1
Lbl D4
ClrHome
Input "n=?",N
Disp "
remainder(N,D)→R
If R=0
Then
Output(3,1,(N/D)*sum(⌊CYC)
Else
Output(3,1,sum(⌊CYC)*iPart(N/D)+sum(⌊CYC,1,R)
End
Pause 
Goto D1
Lbl E0
ClrHome
Menu("Other","Arithmetic # of Terms",E1,"Geometric # of Terms",E2,"Sum of n²",E3,"Sum of n",E4,"Back",M0
Lbl E1
ClrHome
Input "a-1st=?",A
Input "a-Last=?",B
Input "Interval=?",I
Output(5,1,"n="
Output(5,3,(B-A+I)/I
Pause 
Goto E0
Lbl E2
ClrHome
Input "a-1st=?",A
Input "a-Last=?",B
Input "Interval=?",R
Output(5,1,"n="
Output(5,3,(ln(B/A))/ln(R)+1
Pause 
Goto E0
Lbl E3
ClrHome
Input "n=?",N
Disp "
Output(3,1,N(N+1)(2N+1)/6
Pause 
Goto E0
Lbl E4
ClrHome
Input "n=?",N
Disp "
Output(3,1,(N+1)^2*(N^2)/4
Pause 
Goto E0
Lbl E4
Lbl Z0
ClrHome
Stop